!!****m* DoNOF/m_rdmd
!! NAME
!! basic NOFT variables
!!
!! FUNCTION
!! This module contains definitions of common variables used by NOFT module.
!!
!! COPYRIGHT
!! This file is distributed under the terms of the
!! GNU General Public License, see http://www.gnu.org/copyleft/gpl.txt .
!!
!! NOTES
!!
!! SOURCE

module m_rdmd 

 implicit none

!!****t* m_rdmd/rdm_t
!! NAME
!! rdm_t
!!
!! FUNCTION
!! Datatype storing noft quantities and arrays needed
!!
!! SOURCE

 type,public :: rdm_t

  logical::GAMMAs_nread=.true.   ! Are GAMMAS read from previous calc.?
  integer::INOF=7                ! Functional to use (5-> PNOF5, 7-> PNOF7)
  integer::Ista=1                ! Use PNOF7s version
  integer::Nfrozen               ! Number of frozen orbitals in the NOFT calc.
  integer::Nbeta_elect           ! Number of orbitals containing beta electrons
  integer::Nalpha_elect          ! Number of orbitals containing alpha electrons
  integer::Nsingleocc=0          ! Number of singly occ orbitals
  integer::NBF_tot               ! Number of total orbitals
  integer::NBF_occ               ! Number of frozen plus active orbitals
  integer::NBF_ldiag             ! Size of the arrays that contain J and K integrals
  integer::Ncoupled              ! Number of 'virtual' coupled orbital per 'occupied' orbital  
  integer::Npairs                ! Number of electron pairs
  integer::Npairs_p_sing         ! Number of electron pairs plus number of singly occ orbitals
  integer::Ngammas               ! Number of gammas (independet variables used in occ optimization procedure)
  double precision::Sums
! arrays 
  double precision,allocatable,dimension(:)::OCC
  double precision,allocatable,dimension(:)::GAMMAs_old
  double precision,allocatable,dimension(:)::DM2_J,DM2_K
  double precision,allocatable,dimension(:)::Docc_gamma
  double precision,allocatable,dimension(:)::DDM2_gamma_J,DDM2_gamma_K

 contains 
   procedure :: free => rdm_free
   ! Destructor.

   procedure :: print_dmn => print_rdm
   ! Print the 1,2-RDMs into unformated files.

   procedure :: print_orbs => print_orb_coefs
   ! Print orbital coefs to a formated file.

   procedure :: print_gammas => print_gammas_old
   ! Print GAMMAs_old indep./unconstrained variables.

 end type rdm_t

 public :: rdm_init     ! Main creation method.
!!***

CONTAINS  !==============================================================================

!!***
!!****f* DoNOF/rdm_init
!! NAME
!! rdm_init
!!
!! FUNCTION
!!  Initialize the data type rdm_t 
!!
!! INPUTS
!! INOF=PNOFi functional to use
!! Ista=Use PNOF7 (Ista=0) or PNOF7s (Ista=1)
!! NBF_tot=Number of total orbitals
!! NBF_occ=Number of orbitals that are occupied
!! Nfrozen=Number of frozen orbitals that remain with occ=2.0 
!! Npairs=Number of electron pairs
!! Ncoupled=Number of coupled orbitals per electron pair (it is then used as Ncoupled-1 inside this module, as the number
!!             of coupled 'virtual' orbitals to a 'initially occupied' (HF) orbital)
!! Nbeta_elect=Number of beta electrons (N/2 for spin compensated systems)
!! Nalpha_elect=Number of beta electrons (N/2 for spin compensated systems)
!!
!! OUTPUT
!!
!! PARENTS
!!  
!! CHILDREN
!!
!! SOURCE

subroutine rdm_init(RDMd,INOF,Ista,NBF_tot,NBF_occ,Nfrozen,Npairs,&
&  Ncoupled,Nbeta_elect,Nalpha_elect)
!Arguments ------------------------------------
!scalars
 integer,intent(in)::INOF,Ista
 integer,intent(in)::NBF_tot,NBF_occ,Nfrozen,Npairs,Ncoupled
 integer,intent(in)::Nbeta_elect,Nalpha_elect
 type(rdm_t),intent(inout)::RDMd
!Local variables ------------------------------
!scalars
!arrays
!************************************************************************

 RDMd%INOF=INOF
 RDMd%Ista=Ista
 RDMd%Nfrozen=Nfrozen
 RDMd%Nbeta_elect=Nbeta_elect
 RDMd%Nalpha_elect=Nalpha_elect
 RDMd%NBF_occ=NBF_occ
 RDMd%NBF_tot=NBF_tot
 RDMd%Ncoupled=Ncoupled
 RDMd%Npairs=Npairs
 RDMd%Nsingleocc=Nalpha_elect-Nbeta_elect
 RDMd%Npairs_p_sing=RDMd%Npairs+RDMd%Nsingleocc 
 RDMd%NBF_ldiag=RDMd%NBF_occ*(RDMd%NBF_occ+1)/2
 RDMd%Ngammas=RDMd%Ncoupled*RDMd%Npairs
 allocate(RDMd%DM2_J(RDMd%NBF_occ*RDMd%NBF_occ),RDMd%DM2_K(RDMd%NBF_occ*RDMd%NBF_occ)) 
 allocate(RDMd%Docc_gamma(RDMd%NBF_occ*RDMd%Ngammas)) 
 allocate(RDMd%DDM2_gamma_J(RDMd%NBF_occ*RDMd%NBF_occ*RDMd%Ngammas))
 allocate(RDMd%DDM2_gamma_K(RDMd%NBF_occ*RDMd%NBF_occ*RDMd%Ngammas)) 
 allocate(RDMd%GAMMAs_old(RDMd%Ngammas))
 allocate(RDMd%OCC(RDMd%NBF_occ))

end subroutine rdm_init
!!***

!!***
!!****f* DoNOF/rdm_free
!! NAME
!! rdm_free
!!
!! FUNCTION
!!  Free allocated arrays of the data type rdm_t 
!!
!! INPUTS
!!
!! OUTPUT
!!
!! PARENTS
!!  
!! CHILDREN
!!
!! SOURCE

subroutine rdm_free(RDMd)
!Arguments ------------------------------------
!scalars
 class(rdm_t),intent(inout)::RDMd
!Local variables ------------------------------
!scalars
!arrays
!************************************************************************

 deallocate(RDMd%GAMMAs_old)
 deallocate(RDMd%OCC)
 deallocate(RDMd%DM2_J,RDMd%DM2_K) 
 deallocate(RDMd%Docc_gamma) 
 deallocate(RDMd%DDM2_gamma_J)
 deallocate(RDMd%DDM2_gamma_K) 

end subroutine rdm_free
!!***

!!***
!!****f* DoNOF/print_rdm
!! NAME
!! print_rdm
!!
!! FUNCTION
!!  Print the 2-RDM matrix allocated in rdm_t to the binary file DM2 
!!  Print the 1-RDM matrix allocated in rdm_t to the binary file DM1
!!
!! INPUTS
!!
!! OUTPUT
!!
!! PARENTS
!!  
!! CHILDREN
!!
!! SOURCE

subroutine print_rdm(RDMd,DM2_J,DM2_K)
!Arguments ------------------------------------
!scalars
 class(rdm_t),intent(inout)::RDMd
!arrays
 double precision,dimension(RDMd%NBF_occ,RDMd%NBF_occ),intent(in)::DM2_J,DM2_K
!Local variables ------------------------------
!scalars
integer::iorb,iorb1,iunit=312
double precision::tol8=1.0d-8
!arrays

!************************************************************************

 ! Print the 2-RDM
 ! TODO: Missing terms for Nsingleocc>0 !
 open(unit=iunit,form='unformatted',file='DM2')
 do iorb=1,RDMd%NBF_occ
  if(dabs(RDMd%OCC(iorb))>tol8) write(iunit) iorb,iorb,iorb,iorb,RDMd%OCC(iorb)
  do iorb1=1,iorb-1
   if(dabs(DM2_J(iorb,iorb1))>tol8) then
     write(iunit) iorb,iorb1,iorb,iorb1,DM2_J(iorb,iorb1)  
     write(iunit) iorb1,iorb,iorb1,iorb,DM2_J(iorb,iorb1)  
   endif
   if(dabs(DM2_K(iorb,iorb1))>tol8) then
    write(iunit) iorb,iorb1,iorb1,iorb,-DM2_K(iorb,iorb1)
    write(iunit) iorb1,iorb,iorb,iorb1,-DM2_K(iorb,iorb1)
   endif
  enddo
 enddo
 write(iunit) 0,0,0,0,0.0d0
 write(iunit) 0,0,0,0,0.0d0
 close(iunit)

 ! Print the 1-RDM
 open(unit=iunit,form='unformatted',file='DM1')
 do iorb=1,RDMd%NBF_occ
  if(dabs(RDMd%OCC(iorb))>tol8) write(iunit) iorb,iorb,2.0d0*RDMd%OCC(iorb)
 enddo
 write(iunit) 0,0,0.0d0
 close(iunit)

end subroutine print_rdm
!!***

!!***
!!****f* DoNOF/print_orb_coefs
!! NAME
!! print_orb_coefs
!!
!! FUNCTION
!!  Print the orbital coefficients to a given file
!!
!! INPUTS
!!  COEF=Orbital coefs.
!!  name_file=Name of the file where the orb. coefs. are printed
!!
!! OUTPUT
!!
!! PARENTS
!!  
!! CHILDREN
!!
!! SOURCE

subroutine print_orb_coefs(RDMd,COEF,name_file)
!Arguments ------------------------------------
!scalars
 class(rdm_t),intent(in)::RDMd
!arrays
 character(len=10)::name_file 
 double precision,dimension(RDMd%NBF_tot,RDMd%NBF_tot),intent(in)::COEF
!Local variables ------------------------------
!scalars
integer::iorb,iorb1,iorb2,iunit=312
!arrays

!************************************************************************

 ! Print the orb. coefs
 open(unit=iunit,form='formatted',file=name_file)
 do iorb=1,RDMd%NBF_tot
  do iorb1=1,(RDMd%NBF_tot/10)*10,10
   write(iunit,'(f14.8,9f13.8)') COEF(iorb1:iorb1+9,iorb)  
  enddo
  iorb1=(RDMd%NBF_tot/10)*10+1
  write(iunit,'(f14.8,*(f13.8))') COEF(iorb1:,iorb)
 enddo
 close(iunit)

end subroutine print_orb_coefs
!!***

!!***
!!****f* DoNOF/print_gammas_old
!! NAME
!! print_gammas_old
!!
!! FUNCTION
!!  Print the GAMMAs_old vector allocated in rdm_t to the binary file GAMMAS
!!
!! INPUTS
!!
!! OUTPUT
!!
!! PARENTS
!!  
!! CHILDREN
!!
!! SOURCE

subroutine print_gammas_old(RDMd)
!Arguments ------------------------------------
!scalars
 class(rdm_t),intent(inout)::RDMd
!arrays
!Local variables ------------------------------
!scalars
integer::igamma,iunit=312
!arrays

!************************************************************************

 ! Print the GAMMAS_old
 open(unit=iunit,form='unformatted',file='GAMMAS')
 do igamma=1,RDMd%Ngammas
  write(iunit) igamma,RDMd%OCC(igamma)
 enddo
 write(iunit) 0,0.0d0
 close(iunit)

end subroutine print_gammas_old
!!***

end module m_rdmd
!!***
